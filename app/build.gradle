buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'

        // Java 8 support
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'

        // This is necessary so that lint will not fail on lambdas. For more details, see the following links;
        //   https://github.com/evant/gradle-retrolambda/issues/96
        //   https://github.com/evant/android-retrolambda-lombok
        classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
    }

    // Exclude the version of lombok that the android plugin depends on (so that we can use the
    // forked version of retrolambda instead)
    configurations.classpath.exclude group: 'com.android.tools.external.lombok'
}

repositories {
    jcenter()
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.quicksilver.mbeattie.rxperiment"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android Support Libraries
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    compile 'com.android.support:support-v13:23.2.0'
    compile 'com.android.support:support-annotations:23.2.0'

    // Rx Libraries
    compile 'io.reactivex:rxjava:1.1.1'
    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'

    // Dagger 2 Dependency Injection
    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    testCompile 'junit:junit:4.12'

    // Needed to build and run Espresso Tests
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.1') {
        exclude group: 'javax.inject'
    }
    // Needed to specify updated Support Annotations
    androidTestCompile 'com.android.support:support-annotations:23.2.0'
    // Needed for Instrumentation Tests
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    // Needed to use JUnit 4 Rules
    androidTestCompile 'com.android.support.test:rules:0.4.1'
}

retrolambda {
    // Version 22.1.0 of the support library broke retrolambda processing, unless you add the following noverify
    // argument. See the following link for more details: https://github.com/evant/gradle-retrolambda/issues/105
    jvmArgs '-noverify'
}